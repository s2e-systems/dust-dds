plugins {
    id 'com.android.application'
    id "org.mozilla.rust-android-gradle.rust-android" version "0.9.4"
}

android {
    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            def keystoreProperties = new Properties()
            if (keystorePropertiesFile.exists()) {
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    namespace 'com.s2e_systems.dustddsshapesdemo'
    ndkVersion "27.0.11902837"
    compileSdk 35

    defaultConfig {
        applicationId "com.s2e_systems.dustddsshapesdemo"
        minSdk 28
        targetSdk 35
        versionCode 2
        versionName "1.0"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

    cargo {
        module = "."
        targets = ["x86", "x86_64", "arm", "arm64"]
        libname = "shapes_demo_app"
        verbose = true
        prebuiltToolchains = true
        targetDirectory = "../../../target"
        profile = 'release'
    }
}

dependencies {
    preBuild.dependsOn "cargoBuild"
}